plugins{
	id 'java'
	id 'application'
	id 'eclipse'
	id 'com.github.johnrengelman.shadow' version '5.1.0'
	id 'edu.sc.seis.launch4j' version '2.4.6'
}

sourceSets{
	main{
		java{
			srcDirs = ['src']
		}
		resources{
			srcDirs = ['src']
		}
	}
}

javadoc{
	options.memberLevel = JavadocMemberLevel.PRIVATE
}

compileJava.options.encoding = 'UTF-8'
sourceCompatibility = 1.8
version = 'v8.4'//Don't forget Main.java
shadowJar.archiveName = 'KeysPerSecond-' + project.version + '.jar'

repositories{
	//Local cache
	if(project.hasProperty("nexusPublic")){
		maven{
			url "$nexusPublic"
		}
	}
	mavenCentral();
	maven{
		url "https://oss.sonatype.org/content/repositories/snapshots"
	}
}

dependencies{
	implementation 'dev.roanh.util:util:2.0'
	implementation 'com.github.kwhat:jnativehook:2.2-20210915.222335-44'
}

application{
	mainClassName = 'dev.roanh.kps.Main'
}

launch4j{
	mainClassName = application.mainClassName
	icon = "${projectDir}/kps.ico"
	jreMinVersion = project.sourceCompatibility
	outfile = project.name + '-' + project.version + '.exe'
	fileDescription = project.name
	version = project.version.substring(1)
	textVersion = project.version.substring(1)
	copyright = "Roan Hofland"
}